buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath libraries.androidGradle
    }
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.vikki.logs.dialog.confirmdialog.sample"
        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion
        versionCode parent.ext.versionCode
        versionName parent.ext.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

checkstyle {
    configFile project.file('../config/checkstyle/checkstyle.xml')
    showViolations true
}

android.applicationVariants.all { variant ->
    def name = variant.buildType.name

    def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
    checkstyle.dependsOn variant.javaCompile
    checkstyle.source variant.javaCompile.source
    checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
    checkstyle.exclude('**/Manifest.java')
    checkstyle.exclude('**/BuildConfig.java')
    checkstyle.exclude('**/R.java')
    project.tasks.getByName("check").dependsOn checkstyle
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile libraries.androidV7Support
    compile project(':confirmdialog')
}
